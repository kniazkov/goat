/*

Copyright (C) 17..19 Ivan Kniazkov

This file is part of standard library for programming language
codenamed "Goat" ("Goat standard library").

Goat standard library is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat standard library is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat standard library.  If not, see <http://www.gnu.org/licenses/>.

*/

var IndexOutOfBounds = Exception ->
# {
};

var Collection =
# {
    size : $()
    {
        return 0;
    },

    empty : $()
    {
        return this.size() == 0;
    },

    iterator : $()
    {
        return
        {
            valid : $()
            {
                return false;
            },

            next : $()
            {
                return null;
            }
        };
    },

    contains : $(element)
    {
        for (var item in this)
        {
            if (element == item)
                return true;
        }
        return false;
    },

    indexOf : $(element)
    {
        var index = 0;
        for (var item in this)
        {
            if (element == item)
                return index;
            ++index;
        }
        return -1;        
    },

    get : $(index)
    {
        if (index < 0 || index >= this.size())
            throw new IndexOutOfBounds();

        var item;
        for (item in this)
        {
            if (index == 0)
                break;
            index--;
        }
        return item;
    },

    toArray : $()
    {
        var result = [];
        for (var item in this)
        {
            result.push(item);
        }
        return result;
    }
};

var MutableCollection = Collection ->
# {
    clear : $()
    {
        return false;
    }
};
