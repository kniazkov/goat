/*

Copyright (C) 17..19 Ivan Kniazkov

This file is part of standard library for programming language
codenamed "Goat" ("Goat standard library").

Goat standard library is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat standard library is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat standard library.  If not, see <http://www.gnu.org/licenses/>.

*/

import "collection.goat";

var ListItem =
# {
};

var List = Collection ->
# {
    init : $()
    {
        this.first = null;
        this.last = null;
        this.count = 0;
    },

    size : $()
    {
        return count;
    },

    contains : $(element)
    {
        var item = first;
        while(item)
        {
            if (element == item.element)
                return true;
            item = item.next;
        }
        return false;
    },

    indexOf : $(element)
    {
        var index = 0;
        var item = first;
        while(item)
        {
            if (element == item.element)
                return index;
            item = item.next;
            ++index;
        }
        return -1;
    },

    pushf : $(element)
    {
        return this.addFirst(element);
    },

    addFirst : $(element)
    {
	var item = new ListItem();
        item.element = element;
        item.previous = null;
        item.next = first;

        if (first != null)
            first.previous = item;
        else
            last = item;
        first = item;
        count++;
        return item;
    },

    push : $(element)
    {
        return this.addLast(element);
    },

    addLast : $(element)
    {
        var item = new ListItem();
        item.element = element;
        item.previous = last;
        item.next = null;
        
        if (last != null)
            last.next = item;
        else
            first = item;
        last = item;
        count++;
        return item;
    },

    popf : $()
    {
        return this.removeFirst();
    },

    removeFirst : $()
    {
        if (first == null)
            return undefined;
        count--;
        var item = first;
        if (count != 0)
            item.next.previous = null;
        else
            last = null;
        first = item.next;
        return item.element;
    },

    pop : $()
    {
        return this.removeLast();
    },

    removeLast : $()
    {
        if (last == null)
            return undefined;
        count--;
        var item = last;
        if (count != 0)
            item.previous.next = null;
        else
            first = null;
        last = item.previous;
        return item.element;;
    },

    iterator : $()
    {
        return new _Iterator(this, first);
    },

    _Iterator : Iterator ->
    {
        init : $(list, item)
        {
            this.list = list;
            this.item = item;
        },

        valid : $()
        {
            return item != null;
        },

        next : $()
        {
            if (item == null)
                return undefined;
            var element = item.element;
            item = item.next;
            return element;
        }
    }
};
