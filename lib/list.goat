/*

Copyright (C) 2017, 2018 Ivan Kniazkov

This file is part of standard library for programming language
codenamed "Goat" ("Goat standard library").

Goat standard library is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat standard library is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat standard library.  If not, see <http://www.gnu.org/licenses/>.

*/

import "iterator.goat";

var List = Iterable -> {

    init : $() {
        this.first = null;
        this.last = null;
    },

    empty : $() {
        return first == null;
    },

    ListIsEmpty : Exception -> {
    },

    pushf : $(val) {
        var item = {
            prev : null,
            next : first,
            val : val
        };

        if (last != null) {
            first.prev = item;
        }
        else {
            last = item;
        }
        first = item;
    },

    push : $(val) {
        var item = {
            prev : last,
            next : null,
            val : val
        };

        if (first != null) {
            last.next = item;
        }
        else {
            first = item;
        }
        last = item;
    },

    popf : $() {
        if (first == null)
            throw new ListIsEmpty();
        var item = first;
        first = first.next;
        if (first == null)
            last = null;
        return item.val;
    },

    pop : $() {
        if (last == null)
            throw new ListIsEmpty();
        var item = last;
        last = last.prev;
        if (last == null)
            first = null;
        return item.val;
    },

    peekf : $() {
        if (first == null)
            throw new ListIsEmpty();
        return first.val;
    },

    peek : $() {
        if (last == null)
            throw new ListIsEmpty();
        return last.val;
    },

    iterator : $() {
        return new List.Iterator(first);
    },
    
    Iterator : Iterator -> {
        
        item : null,
        
        init : $(item) {
            this.item = item;
            this.following = item?.next || null;
        },
        
        valid : $() {
            return item != null;
        },
        
        next : $() {
            if (!item)
                throw new Exception.InvalidOperation();
            item = following;
            following = item?.next || null;
        },
        
        read : $() {
            if (!item)
                throw new Exception.InvalidOperation();
            return item.val;
        },
        
        write : $(val) {
            if (!item)
                throw new Exception.InvalidOperation();
            item.val = val;
        }
    }
};