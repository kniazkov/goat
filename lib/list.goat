/*

Copyright (C) 17..19 Ivan Kniazkov

This file is part of standard library for programming language
codenamed "Goat" ("Goat standard library").

Goat standard library is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat standard library is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat standard library.  If not, see <http://www.gnu.org/licenses/>.

*/

import "collection.goat";

var List = Collection ->
{
    init : $()
    {
        this.first = null;
        this.last = null;
        this.count = 0;
    },

    size : $()
    {
        return count;
    },

    pushf : $(item)
    {
        this.addFirst(item);
    },

    addFirst : $(item)
    {
        item[this] =
        {
            list : this,
            previous : null,
            next : first
        };
        if (first != null)
            first[this].previous = item;
        else
            last = item;
        first = item;
        count++;
    },

    push : $(item)
    {
        this.addLast(item);
    },

    addLast : $(item)
    {
        item[this] =
        {
            list : this,
            previous : last,
            next : null
        };
        if (last != null)
            last[this].next = item;
        else
            first = item;
        last = item;
        count++;
    },

    popf : $()
    {
        return this.removeFirst();
    },

    removeFirst : $()
    {
        if (first == null)
            return undefined;
        count--;
        var item = first;
        if (count != 0)
            item[this].next[this].previous = null;
        else
            last = null;
        first = item[this].next;
        item[this].next = null;
        return item;
    },

    pop : $()
    {
        return this.removeLast();
    },

    removeLast : $()
    {
        if (last == null)
            return undefined;
        count--;
        var item = last;
        if (count != 0)
            item[this].previous[this].next = null;
        else
            first = null;
        last = item[this].previous;
        item[this].previous = null;
        return item;
    },

    iterator : $()
    {
        return new _Iterator(this, first);
    },

    _Iterator : Iterator ->
    {
        init : $(list, item)
        {
            this.list = list;
            this.item = item;
        },

        valid : $()
        {
            return item != null;
        },

        next : $()
        {
            if (item == null)
                return undefined;
            var value = item;
            item = item[list].next;
            return value;
        }
    }
};
