/*

Copyright (C) 17..19 Ivan Kniazkov

This file is part of standard library for programming language
codenamed "Goat" ("Goat standard library").

Goat standard library is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat standard library is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat standard library.  If not, see <http://www.gnu.org/licenses/>.

*/

import "collection.goat";

var List = MutableCollection ->
# {
    init : $(data)
    {
        this.first = null;
        this.last = null;
        this.count = 0;
        
        this.ListItem = {};

        if (data.instanceof(Collection) || data.instanceof(Array))
        {
            for (var element in data)
                this.addLast(element);
        }
    },

    size : $()
    {
        return count;
    },

    empty : $()
    {
        return count == 0;
    },

    clear : $()
    {
        first = null;
        last = null;
        count = 0;
    },

    find : $(element)
    {
        var item = first;
        while(item)
        {
            if (element == item.element)
                return item;
            item = item.next;
        }
        return null;
    },

    contains : $(element)
    {
	return !!this.find(element);
    },

    indexOf : $(element)
    {
        var index = 0;
        var item = first;
        while(item)
        {
            if (element == item.element)
                return index;
            item = item.next;
            ++index;
        }
        return -1;
    },

    pushf : $(element)
    {
        return this.addFirst(element);
    },

    addFirst : $(element)
    {
	var item = new ListItem();
        item.element = element;
        item.previous = null;
        item.next = first;

        if (first != null)
            first.previous = item;
        else
            last = item;
        first = item;
        count++;
        return item;
    },

    push : $(element)
    {
        return this.addLast(element);
    },

    addLast : $(element)
    {
        var item = new ListItem();
        item.element = element;
        item.previous = last;
        item.next = null;
        
        if (last != null)
            last.next = item;
        else
            first = item;
        last = item;
        count++;
        return item;
    },

    insertBefore : $(element, before)
    {
        if (!before)
            return this.addLast(element);

        if (!before.instanceof(ListItem))
            throw new Exception.IllegalArgument();

        var item = new ListItem();
        item.element = element;
        item.previous = before.previous;
        item.next = before;

        if (before.previous != null)
            before.previous.next = item;
        else
            first = item;
        before.previos = item;
        count++;
        return item;
    },

    insertAfter : $(element, after)
    {
        if (!after)
            return this.addFirst(element);

        if (!after.instanceof(ListItem))
            throw new Exception.IllegalArgument();

        var item = new ListItem();
        item.element = element;
        item.next = after.next;
        item.previous = after;

        if (after.next != null)
            after.next.previous = item;
        else
            last = item;
        after.next = item;
        count++;
        return item;
    },

    popf : $()
    {
        return this.removeFirst();
    },

    removeFirst : $()
    {
        if (first == null)
            return undefined;
        count--;
        var item = first;
        if (count != 0)
            item.next.previous = null;
        else
            last = null;
        first = item.next;
        return item.element;
    },

    pop : $()
    {
        return this.removeLast();
    },

    removeLast : $()
    {
        if (last == null)
            return undefined;
        count--;
        var item = last;
        if (count != 0)
            item.previous.next = null;
        else
            first = null;
        last = item.previous;
        return item.element;;
    },

    remove : $(item)
    {
        if (!item.instanceof(ListItem))
            throw new Exception.IllegalArgument();

        if (item.previous != null)
            item.previous.next = item.next;
        else
            first = item.next;

        if (item.next != null)
            item.next.previous = item.previous;
        else
            last = item.previous;

        count--;
    },

    peekf : $()
    {
        return first?.element;
    },

    getFirst : $()
    {
        return first?.element;
    },

    peek : $()
    {
        return last?.element;
    },

    getLast : $()
    {
        return last?.element;
    },

    iterator : $()
    {
        return new _Iterator(this, first);
    },

    _Iterator : Iterator ->
    {
        init : $(list, item)
        {
            this.list = list;
            this.item = item;
        },

        valid : $()
        {
            return item != null;
        },

        next : $()
        {
            if (item == null)
                return undefined;
            var element = item.element;
            item = item.next;
            return element;
        }
    }
};
