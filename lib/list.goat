/*

Copyright (C) 17..19 Ivan Kniazkov

This file is part of standard library for programming language
codenamed "Goat" ("Goat standard library").

Goat standard library is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat standard library is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat standard library.  If not, see <http://www.gnu.org/licenses/>.

*/

import "collection.goat";

var List = Collection ->
{
    init : $()
    {
        this.id = uid();
        this.first = null;
        this.last = null;
        this.count = 0;
    },

    size : $()
    {
        return count;
    },

    push : $(item)
    {
        item[id] =
        {
            list : this,
            previous : last,
            next : null
        };
        if (last != null)
            last[id].next = item;
        else
            first = item;
        last = item;
        count++;
    },

    pushf : $(item)
    {
        item[id] =
        {
            list : this,
            previous : null,
            next : first
        };
        if (first != null)
            first[id].previous = item;
        else
            last = item;
        first = item;
        count++;
    },

    pop : $()
    {
        if (last == null)
            return undefined;
        count--;
        var item = last;
        if (count != 0)
            item[id].previous[id].next = null;
        else
            first = null;
        last = item[id].previous;
        item[id].previous = null;
        return item;
    },

    popf : $()
    {
        if (first == null)
            return undefined;
        count--;
        var item = first;
        if (count != 0)
            item[id].next[id].previous = null;
        else
            last = null;
        first = item[id].next;
        item[id].next = null;
        return item;
    },

    iterator : $()
    {
        return new _Iterator(id, first);
    },

    _Iterator : Iterator ->
    {
        init : $(id, item)
        {
            this.id = id;
            this.item = item;
        },

        valid : $()
        {
            return item != null;
        },

        next : $()
        {
            if (item == null)
                return undefined;
            var value = item;
            item = item[id].next;
            return value;
        }
    }
};
