/*

Copyright (C) 2017-2020 Ivan Kniazkov

This file is part of standard library for programming language
codenamed "Goat" ("Goat standard library").

Goat standard library is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat standard library is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat standard library.  If not, see <http://www.gnu.org/licenses/>.

*/

import "mutex.goat";

var Console =
{
    _print : print,
    _println : println,
    _column : 0,
    _latch : new Mutex(),
    _lastThread : null,

    print : $(data)
    {
        _latch.lock();
        var str = String.valueof(data);
        for (var ch in str)
        {
            if (ch == '\r' || ch == '\n')
                _column = 0;
            else
                _column++;
        }
        _print(str);
        _lastThread = Thread.current();
        _latch.unlock();
    },

    println : $(data)
    {
        _latch.lock();
        _println(data);
        _column = 0;
        _lastThread = Thread.current();
        _latch.unlock();
    },

    cret : $()
    {
        _latch.lock();
        var currentThread = Thread.current();
        if (currentThread == _lastThread)
        {
            if (_column > 0)
            {
                _print('\r');
                for (var i = 0; i < _column; i++)
                    _print(' ');
                _print('\r');
                _column = 0;
            }
        }
        else
        {
            _lastThread = currentThread;
            if (_column > 0)
            {
                _print('\n');
                _column = 0;
            }
        }
        _latch.unlock();
    }
};
