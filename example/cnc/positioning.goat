/*

Copyright (C) 2017-2020 Ivan Kniazkov

This file is part of interpreter of programming language
codenamed "Goat" ("Goat interpreter").

Goat interpreter is free software: you can redistribute it and/or
modify it under the terms of the GNU General Public License as published
by the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Goat interpreter is distributed in the hope that it will be
useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License along
with Goat interpreter.  If not, see <http://www.gnu.org/licenses/>.

*/

import "stepper_motor.goat";

var Positioning =
# {
    init : $(axisX, axisY, axisZ, resolution)
    {
        if (!axisX.instanceof(StepperMotor) || !axisY.instanceof(StepperMotor) || !axisZ.instanceof(StepperMotor))
            throw new Exception.IllegalArgument();

        this.axisX = axisX;
        this.axisY = axisY;
        this.axisZ = axisZ;
        this.minDistance = 1.0 / resolution;
    },

    moveTo : $(X, Y, Z, V)
    {
        if (V <= 0)
            throw new Exception.IllegalArgument();

        while(true)
        {
            // current position
            var cX = axisX.getPosition();
            var cY = axisY.getPosition();
            var cZ = axisZ.getPosition();

            // delta
            var dX = X - cX;
            var dY = Y - cY;
            var dZ = Z - cZ;

            // distance
            var l = sqrt(dX * dX + dY * dY + dZ * dZ);
            if (l <= minDistance)
                return;

            // time
            var t = l / V;

            // speed for each axis
            var vX = dX / t;
            var vY = dY / t;
            var vZ = dZ / t;

            // set 
            axisX.setVelocity(vX);
            axisY.setVelocity(vY);
            axisZ.setVelocity(vZ);
        }
    }
};
