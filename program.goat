//exit();

import "stepper_motor.goat";
import "positioning.goat";

ports.init();

var smpX = new StepperMotorParameters();
smpX.gpioStep = "gpio0";
smpX.scalingFactor = 10.0;
var axisX = new StepperMotor(smpX);

var smpY = new StepperMotorParameters();
smpY.gpioStep = "gpio1";
smpY.scalingFactor = 10.0;
var axisY = new StepperMotor(smpY);

var smpZ = new StepperMotorParameters();
smpZ.gpioStep = "gpio2";
smpZ.scalingFactor = 10.0;
var axisZ = new StepperMotor(smpZ);

var positioning = new Positioning(axisX, axisY, axisZ, 3.0);

import "gcode.goat";
import "io.goat";
import "interpolator.goat";
import "bitmap.goat";
import "color.goat";

//var source = "g00 x100 y100\ng02 x300 i100";
var raw = load("gcode.txt");
var source = raw.decode("utf8");
var gcode = GCode.parse(source);
gcode = GCode.optimize(gcode, 5.0);
var p0 = new Interpolator.Point();
p0.X = 0.0;
p0.Y = 0.0;
p0.Z = 0.0;
var points = Interpolator.calculate(gcode, p0, 5.0, 15.0, 15.0);

var bmp = new Bmp24(2000, 1500);
positioning.run(points, $(p1)
{
    Console.cret();
    if (p1.percent == 100)
    {
        Console.println(p1.instruction.toString());
    }
    else
    {
        Console.print("\'" + p1.instruction.toString() + "\', " + round(p1.percent) +
            "%, X: " + round(100 * axisX.getPosition()) / 100.0 + ", Y: " + round(100 * axisY.getPosition()) / 100.0 + 
            ", V: " + p1.V);
    }
    var c = new Color();
    c.r = p1.V / 15.0;
    c.b = 1.0 - c.r;
    bmp.drawLine(round(p0.X * 5), 1500 - round(p0.Y * 5), round(p1.X * 5), 1500 - round(p1.Y * 5), c);
    p0 = p1;
});
Console.println("Done.");
var arr = bmp.toByteArray();
var file = open("image.bmp", File.Mode.WRITE);
file.write(arr);
file.close();
